// todo Add swarm tests for top scenarios, including the scalar at root level scenario.
// todo Create a new language, similar to this, except using pattern matching instead of prefix notation.

// Cell Parsers
anyCell
keywordCell
 paint keyword
stringCell
 paint string
booleanCell
 enum true false
 paint constant.numeric
numberCell
 paint constant.numeric

// Line Parsers
dugParser
 root
 description A demonstration prefix Language that compiles to JSON.
 inScope abstractValueParser
 catchAllParser errorParser
 javascript
  compile() {
   const res = super.compile()
   return JSON.stringify(JSON.parse(res), null, 2)
  }
abstractValueParser
 cells keywordCell
 cruxFromId
nullParser
 compiler
  stringTemplate null
 extends abstractValueParser
numberParser
 extends abstractValueParser
 cells keywordCell numberCell
 compiler
  stringTemplate {numberCell}
stringParser
 catchAllCellType stringCell
 compiler
  stringTemplate "{stringCell}"
 extends abstractValueParser
booleanParser
 extends abstractValueParser
 cells keywordCell booleanCell
 compiler
  stringTemplate {booleanCell}
objectParser
 catchAllParser memberParser
 extends abstractValueParser
 compiler
  stringTemplate  
  joinSubparticlesWith , 
  openSubparticles {
  closeSubparticles }
arrayParser
 extends abstractValueParser
 inScope abstractValueParser
 compiler
  stringTemplate  
  joinSubparticlesWith , 
  openSubparticles [
  closeSubparticles ]
memberParser
 inScope abstractValueParser
 compiler
  stringTemplate "{stringCell}" :
 cells stringCell
errorParser
 baseParser errorParser