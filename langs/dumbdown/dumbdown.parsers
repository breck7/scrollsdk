// Atom Parsers
anyAtom
blankAtom
dashAtom
 paint constant.language
codeAtom
 paint comment
keywordAtom
 paint keyword
textAtom
 paint string
urlAtom
 paint constant.language

// Line Parsers
errorParser
 baseParser errorParser
dumbdownParser
 extensions dd dumbdown
 description A Language that compiles to HTML. An alternative to Markdown.
 root
 inScope abstractTopLevelParser blankLineParser
 catchAllParser quickParagraphParser
 example
  title Hello world
  title2 This is Dumbdown
  
  paragraph
   It compiles to HTML. Blank lines get turned into brs.
  link https://particles.scroll.pub Dumbdown is a Language.
  list
   - It has lists
   - Too!
  code
   // You can add code as well.
   print("Hello world")
abstractTopLevelParser
 atoms keywordAtom
linkParser
 atoms keywordAtom urlAtom
 catchAllAtomType textAtom
 extends abstractTopLevelParser
 compiler
  stringTemplate <a href="{urlAtom}">{textAtom}</a>
 cue link
paragraphParser
 catchAllParser paragraphContentParser
 extends abstractTopLevelParser
 cue paragraph
 compiler
  openSubparticles <p>
  closeSubparticles </p>
  stringTemplate 
paragraphContentParser
 inScope paragraphContentParser
 catchAllAtomType textAtom
 compiler
  stringTemplate {textAtom}
codeParser
 description A code block.
 catchAllParser lineOfCodeParser
 extends abstractTopLevelParser
 javascript
  compile() {
   return `<code>${this.indentation + this.subparticlesToString()}</code>`
  }
 cue code
listParser
 inScope dashParser
 extends abstractTopLevelParser
 compiler
  stringTemplate 
  openSubparticles <ul>
  closeSubparticles </ul>
 cue list
blankLineParser
 description Blank lines compile to nothing in the HTML.
 atoms blankAtom
 compiler
  stringTemplate 
 pattern ^$
 tags doNotSynthesize
lineOfCodeParser
 catchAllAtomType codeAtom
 catchAllParser lineOfCodeParser
dashParser
 cue -
 catchAllAtomType textAtom
 compiler
  stringTemplate <li>{textAtom}</li>
 atoms dashAtom
titleParser
 catchAllAtomType textAtom
 extends abstractTopLevelParser
 compiler
  stringTemplate 
 cue title
 javascript
  compile(spaces) {
   const title = this.content
   const permalink = Utils.stringToPermalink(this.content)
   return `<h1 id="${permalink}"><a href="#${permalink}">${title}</a></h1>`
  }
title2Parser
 catchAllAtomType textAtom
 extends abstractTopLevelParser
 compiler
  stringTemplate <h2>{textAtom}</h2>
 cue title2
title3Parser
 extends title2Parser
 compiler
  stringTemplate <h3>{textAtom}</h3>
 cue title3
title4Parser
 extends title2Parser
 compiler
  stringTemplate <h4>{textAtom}</h4>
 cue title4
title5Parser
 extends title2Parser
 compiler
  stringTemplate <h5>{textAtom}</h5>
 cue title5
title6Parser
 extends title2Parser
 compiler
  stringTemplate <h6>{textAtom}</h6>
 cue title6
quickParagraphParser
 catchAllAtomType textAtom
 compiler
  stringTemplate <p>{textAtom}</p>