// todo Explore best ways to add polymorphism

// Atom Parsers
anyAtom
booleanAtom
 enum false true
filepathAtom
identifierAtom
 regex [$A-Za-z_][0-9a-zA-Z_$]*
 paint variable
 examples myVarA someVarB
numberAtom
 regex \-?[0-9]*\.?[0-9]*
 paint constant.numeric
numberIdentifierAtom
 extends identifierAtom
hashBangAtom
 paint comment
hashBangKeywordAtom
 paint comment
stringAtom
 paint string
booleanIdentifierAtom
 extends identifierAtom
functionIdentifierAtom
 extends identifierAtom
identifiersAtom
 extends identifierAtom
instanceIdentifierAtom
 extends identifierAtom
methodIdentifierAtom
 extends identifierAtom
resultIdentifierAtom
 extends identifierAtom
cueAtom
 paint keyword
stringIdentifierAtom
 extends identifierAtom
stringAtomsAtom
 extends stringAtom
leftNumberAtom
 extends numberAtom
leftAnyAtom
 extends anyAtom

// Line Parsers
fireParser
 root
 description A useless prefix Language that compiles to Javascript for testing Particles features.
 inScope hashbangParser abstractTerminalParser abstractNonTerminalParser
 catchAllParser errorParser
 javascript
  async execute() {
   let outputLines = []
   const _originalConsoleLog = console.log
   const tempConsoleLog = (...params) => outputLines.push(params)
   console.log = tempConsoleLog
   const compiled = this.compile("js")
   eval(compiled)
   console.log = _originalConsoleLog
   console.log(outputLines.join("\n"))
   return outputLines
  }
abstractNonTerminalParser
 inScope abstractTerminalParser abstractNonTerminalParser
 atoms cueAtom
abstractJsblockParser
 compiler
  openSubparticles  {
  closeSubparticles }
 extends abstractNonTerminalParser
blockParser
 description block of code
 popularity .2
 compiler
  stringTemplate /* {identifierAtom} */
 extends abstractJsblockParser
 cue block
functionParser
 cue function
 description Function Assignment
 atoms cueAtom functionIdentifierAtom
 catchAllAtomType anyAtom
 compiler
  stringTemplate const {functionIdentifierAtom} = ({anyAtom}) =>
  catchAllAtomDelimiter , 
 popularity .1
 extends abstractJsblockParser
ifParser
 cue if
 description If tile
 atoms cueAtom identifierAtom
 popularity .2
 compiler
  stringTemplate if ({identifierAtom})
 extends abstractJsblockParser
whileParser
 cue while
 description While tile
 atoms cueAtom identifierAtom
 popularity .1
 compiler
  stringTemplate while ({identifierAtom})
 extends abstractJsblockParser
abstractTerminalParser
 atoms cueAtom
abstractAssignmentParser
 extends abstractTerminalParser
abstractArithmeticParser
 atoms cueAtom identifierAtom
 catchAllAtomType anyAtom
 compiler
  stringTemplate const {identifierAtom} = {anyAtom}
 popularity .2
 extends abstractAssignmentParser
divideParser
 description Divide Numbers
 compiler
  catchAllAtomDelimiter  / 
 extends abstractArithmeticParser
 cue divide
moduloParser
 description Modulo Numbers
 compiler
  catchAllAtomDelimiter %
 extends abstractArithmeticParser
 cue modulo
multiplyParser
 description Multiply Numbers
 compiler
  catchAllAtomDelimiter  * 
 extends abstractArithmeticParser
 cue multiply
substractParser
 description Subtract Numbers
 compiler
  catchAllAtomDelimiter  - 
 extends abstractArithmeticParser
 cue substract
addParser
 cue add
 example
  add ten 2 3 5
 description Add numbers and store result
 compiler
  catchAllAtomDelimiter  + 
 extends abstractArithmeticParser
abstractBooleanOperatorParser
 description Runs a boolean test and saves result.
 extends abstractAssignmentParser
greaterThanParser
 description Greater than test
 atoms cueAtom identifierAtom leftNumberAtom numberAtom
 compiler
  stringTemplate const {identifierAtom} = {leftNumberAtom} > {numberAtom}
 popularity .1
 extends abstractBooleanOperatorParser
 cue greaterThan
greaterThanOrEqualParser
 description Greater than or equal to test
 atoms cueAtom identifierAtom leftNumberAtom numberAtom
 compiler
  stringTemplate const {identifierAtom} = {leftNumberAtom} >= {numberAtom}
 popularity .1
 extends abstractBooleanOperatorParser
 cue greaterThanOrEqual
lessThanParser
 description Less than test
 atoms cueAtom identifierAtom leftAnyAtom anyAtom
 compiler
  stringTemplate const {identifierAtom} = {leftAnyAtom} < {anyAtom}
 popularity .1
 extends abstractBooleanOperatorParser
 cue lessThan
lessThanOrEqualParser
 cue lessThanOrEqual
 description Less than or equal to test
 atoms cueAtom identifierAtom leftAnyAtom anyAtom
 compiler
  stringTemplate const {identifierAtom} = {leftAnyAtom} <= {anyAtom}
 popularity .1
 extends abstractBooleanOperatorParser
sumParser
 cue sum
 description Add numbers and store result
 atoms cueAtom numberIdentifierAtom
 catchAllAtomType numberAtom
 compiler
  stringTemplate const {numberIdentifierAtom} = [{numberAtom}].reduce((sum, num) => sum + num)
  catchAllAtomDelimiter , 
 popularity .1
 extends abstractAssignmentParser
booleanParser
 cue boolean
 description Boolean Assignment
 atoms cueAtom booleanIdentifierAtom booleanAtom
 compiler
  stringTemplate const {booleanIdentifierAtom} = {booleanAtom}
 extends abstractAssignmentParser
callFunctionAndSetParser
 cue callFunctionAndSet
 description Function Call
 popularity .5
 atoms cueAtom resultIdentifierAtom functionIdentifierAtom
 catchAllAtomType anyAtom
 compiler
  stringTemplate const {resultIdentifierAtom} = {functionIdentifierAtom}({anyAtom})
  catchAllAtomDelimiter , 
 extends abstractAssignmentParser
callMethodAndSetParser
 cue callMethodAndSet
 description Method Call
 popularity .5
 atoms cueAtom resultIdentifierAtom instanceIdentifierAtom methodIdentifierAtom
 catchAllAtomType anyAtom
 compiler
  stringTemplate const {resultIdentifierAtom} = {instanceIdentifierAtom}.{methodIdentifierAtom}({anyAtom})
  catchAllAtomDelimiter , 
 extends abstractAssignmentParser
joinParser
 cue join
 description Join strings to form new string
 atoms cueAtom identifierAtom
 catchAllAtomType identifiersAtom
 compiler
  stringTemplate const {identifierAtom} = [{identifiersAtom}].join("")
  catchAllAtomDelimiter , 
 popularity .2
 extends abstractAssignmentParser
mutableNumberParser
 cue mutableNumber
 description Mutable Number Assignment
 atoms cueAtom identifierAtom numberAtom
 compiler
  stringTemplate let {identifierAtom} = {numberAtom}
 extends abstractAssignmentParser
numberParser
 cue number
 description Number Assignment
 atoms cueAtom identifierAtom numberAtom
 compiler
  stringTemplate const {identifierAtom} = {numberAtom}
 popularity .3
 extends abstractAssignmentParser
numbersParser
 cue numbers
 description Number Array Assignment
 atoms cueAtom identifierAtom
 catchAllAtomType numberAtom
 popularity .4
 compiler
  stringTemplate const {identifierAtom} = [{numberAtom}]
  catchAllAtomDelimiter , 
 extends abstractAssignmentParser
stringParser
 cue string
 description String Assignment
 atoms cueAtom stringIdentifierAtom
 catchAllAtomType anyAtom
 compiler
  stringTemplate const {stringIdentifierAtom} = "{anyAtom}"
 popularity .2
 extends abstractAssignmentParser
callFunctionParser
 cue callFunction
 description Function call ignore result.
 popularity .1
 atoms cueAtom functionIdentifierAtom
 catchAllAtomType anyAtom
 compiler
  stringTemplate {functionIdentifierAtom}({anyAtom})
  catchAllAtomDelimiter , 
 extends abstractTerminalParser
decrementParser
 cue decrement
 description Decrement
 atoms cueAtom numberIdentifierAtom
 compiler
  stringTemplate {numberIdentifierAtom}--
 popularity .1
 extends abstractTerminalParser
dumpIdentifierParser
 cue dumpIdentifier
 description Dump variable(s) to console
 catchAllAtomType identifierAtom
 compiler
  stringTemplate console.log({identifierAtom})
  catchAllAtomDelimiter , 
 popularity .5
 extends abstractTerminalParser
exportParser
 cue export
 description Export This
 atoms cueAtom identifierAtom
 compiler
  stringTemplate module.exports = {identifierAtom}
 popularity .1
 extends abstractTerminalParser
incrementParser
 cue increment
 description Increment
 popularity .3
 atoms cueAtom numberIdentifierAtom
 compiler
  stringTemplate {numberIdentifierAtom}++
 extends abstractTerminalParser
printNumberParser
 cue printNumber
 extends abstractTerminalParser
 catchAllAtomType numberIdentifierAtom
 compiler
  stringTemplate console.log({numberIdentifierAtom})
printStringParser
 cue printString
 // todo Allow printing of multiline strings
 extends abstractTerminalParser
 catchAllAtomType stringAtomsAtom
 compiler
  stringTemplate console.log("{stringAtoms}")
requireParser
 cue require
 description Require Something
 atoms cueAtom identifierAtom filepathAtom
 compiler
  stringTemplate const {identifierAtom} = require("{filepathAtom}")
 popularity .1
 extends abstractTerminalParser
returnParser
 cue return
 atoms cueAtom anyAtom
 compiler
  stringTemplate return {anyAtom}
 popularity .1
 extends abstractTerminalParser
hashbangParser
 cue #!
 description Standard bash hashbang line.
 catchAllAtomType hashBangAtom
 compiler
  stringTemplate // #! {hashBangAtom}
 atoms hashBangKeywordAtom
errorParser
 baseParser errorParser
 compiler
  stringTemplate // error