// todo Explore best ways to add polymorphism

// Atom Parsers
anyAtom
booleanAtom
 enum false true
filepathAtom
identifierAtom
 regex [$A-Za-z_][0-9a-zA-Z_$]*
 paint variable
 examples myVarA someVarB
numberAtom
 regex \-?[0-9]*\.?[0-9]*
 paint constant.numeric
numberIdentifierAtom identifierAtom
hashBangAtom
 paint comment
hashBangKeywordAtom
 paint comment
stringAtom
 paint string
booleanIdentifierAtom identifierAtom
functionIdentifierAtom identifierAtom
identifiersAtom identifierAtom
instanceIdentifierAtom identifierAtom
methodIdentifierAtom identifierAtom
resultIdentifierAtom identifierAtom
cueAtom
 paint keyword
stringIdentifierAtom identifierAtom
stringAtomsAtom stringAtom
leftNumberAtom numberAtom
leftAnyAtom anyAtom

// Line Parsers
fireParser
 root
 description A useless prefix Language that compiles to Javascript for testing Particles features.
 inScope hashbangParser abstractTerminalParser abstractNonTerminalParser
 catchAllParser errorParser
 javascript
  async execute() {
   let outputLines = []
   const _originalConsoleLog = console.log
   const tempConsoleLog = (...params) => outputLines.push(params)
   console.log = tempConsoleLog
   const compiled = this.compile("js")
   eval(compiled)
   console.log = _originalConsoleLog
   console.log(outputLines.join("\n"))
   return outputLines
  }
abstractNonTerminalParser
 inScope abstractTerminalParser abstractNonTerminalParser
 atoms cueAtom
abstractJsblockParser abstractNonTerminalParser
 compiler
  openSubparticles  {
  closeSubparticles }
blockParser abstractJsblockParser
 description block of code
 popularity .2
 compiler
  stringTemplate /* {identifierAtom} */
 cue block
functionParser abstractJsblockParser
 cue function
 description Function Assignment
 atoms cueAtom functionIdentifierAtom
 catchAllAtomType anyAtom
 compiler
  stringTemplate const {functionIdentifierAtom} = ({anyAtom}) =>
  catchAllAtomDelimiter , 
 popularity .1
ifParser abstractJsblockParser
 cue if
 description If tile
 atoms cueAtom identifierAtom
 popularity .2
 compiler
  stringTemplate if ({identifierAtom})
whileParser abstractJsblockParser
 cue while
 description While tile
 atoms cueAtom identifierAtom
 popularity .1
 compiler
  stringTemplate while ({identifierAtom})
abstractTerminalParser
 atoms cueAtom
abstractAssignmentParser abstractTerminalParser
abstractArithmeticParser abstractAssignmentParser
 atoms cueAtom identifierAtom
 catchAllAtomType anyAtom
 compiler
  stringTemplate const {identifierAtom} = {anyAtom}
 popularity .2
divideParser abstractArithmeticParser
 description Divide Numbers
 compiler
  catchAllAtomDelimiter  / 
 cue divide
moduloParser abstractArithmeticParser
 description Modulo Numbers
 compiler
  catchAllAtomDelimiter %
 cue modulo
multiplyParser abstractArithmeticParser
 description Multiply Numbers
 compiler
  catchAllAtomDelimiter  * 
 cue multiply
substractParser abstractArithmeticParser
 description Subtract Numbers
 compiler
  catchAllAtomDelimiter  - 
 cue substract
addParser abstractArithmeticParser
 cue add
 example
  add ten 2 3 5
 description Add numbers and store result
 compiler
  catchAllAtomDelimiter  + 
abstractBooleanOperatorParser abstractAssignmentParser
 description Runs a boolean test and saves result.
greaterThanParser abstractBooleanOperatorParser
 description Greater than test
 atoms cueAtom identifierAtom leftNumberAtom numberAtom
 compiler
  stringTemplate const {identifierAtom} = {leftNumberAtom} > {numberAtom}
 popularity .1
 cue greaterThan
greaterThanOrEqualParser abstractBooleanOperatorParser
 description Greater than or equal to test
 atoms cueAtom identifierAtom leftNumberAtom numberAtom
 compiler
  stringTemplate const {identifierAtom} = {leftNumberAtom} >= {numberAtom}
 popularity .1
 cue greaterThanOrEqual
lessThanParser abstractBooleanOperatorParser
 description Less than test
 atoms cueAtom identifierAtom leftAnyAtom anyAtom
 compiler
  stringTemplate const {identifierAtom} = {leftAnyAtom} < {anyAtom}
 popularity .1
 cue lessThan
lessThanOrEqualParser abstractBooleanOperatorParser
 cue lessThanOrEqual
 description Less than or equal to test
 atoms cueAtom identifierAtom leftAnyAtom anyAtom
 compiler
  stringTemplate const {identifierAtom} = {leftAnyAtom} <= {anyAtom}
 popularity .1
sumParser abstractAssignmentParser
 cue sum
 description Add numbers and store result
 atoms cueAtom numberIdentifierAtom
 catchAllAtomType numberAtom
 compiler
  stringTemplate const {numberIdentifierAtom} = [{numberAtom}].reduce((sum, num) => sum + num)
  catchAllAtomDelimiter , 
 popularity .1
booleanParser abstractAssignmentParser
 cue boolean
 description Boolean Assignment
 atoms cueAtom booleanIdentifierAtom booleanAtom
 compiler
  stringTemplate const {booleanIdentifierAtom} = {booleanAtom}
callFunctionAndSetParser abstractAssignmentParser
 cue callFunctionAndSet
 description Function Call
 popularity .5
 atoms cueAtom resultIdentifierAtom functionIdentifierAtom
 catchAllAtomType anyAtom
 compiler
  stringTemplate const {resultIdentifierAtom} = {functionIdentifierAtom}({anyAtom})
  catchAllAtomDelimiter , 
callMethodAndSetParser abstractAssignmentParser
 cue callMethodAndSet
 description Method Call
 popularity .5
 atoms cueAtom resultIdentifierAtom instanceIdentifierAtom methodIdentifierAtom
 catchAllAtomType anyAtom
 compiler
  stringTemplate const {resultIdentifierAtom} = {instanceIdentifierAtom}.{methodIdentifierAtom}({anyAtom})
  catchAllAtomDelimiter , 
joinParser abstractAssignmentParser
 cue join
 description Join strings to form new string
 atoms cueAtom identifierAtom
 catchAllAtomType identifiersAtom
 compiler
  stringTemplate const {identifierAtom} = [{identifiersAtom}].join("")
  catchAllAtomDelimiter , 
 popularity .2
mutableNumberParser abstractAssignmentParser
 cue mutableNumber
 description Mutable Number Assignment
 atoms cueAtom identifierAtom numberAtom
 compiler
  stringTemplate let {identifierAtom} = {numberAtom}
numberParser abstractAssignmentParser
 cue number
 description Number Assignment
 atoms cueAtom identifierAtom numberAtom
 compiler
  stringTemplate const {identifierAtom} = {numberAtom}
 popularity .3
numbersParser abstractAssignmentParser
 cue numbers
 description Number Array Assignment
 atoms cueAtom identifierAtom
 catchAllAtomType numberAtom
 popularity .4
 compiler
  stringTemplate const {identifierAtom} = [{numberAtom}]
  catchAllAtomDelimiter , 
stringParser abstractAssignmentParser
 cue string
 description String Assignment
 atoms cueAtom stringIdentifierAtom
 catchAllAtomType anyAtom
 compiler
  stringTemplate const {stringIdentifierAtom} = "{anyAtom}"
 popularity .2
callFunctionParser abstractTerminalParser
 cue callFunction
 description Function call ignore result.
 popularity .1
 atoms cueAtom functionIdentifierAtom
 catchAllAtomType anyAtom
 compiler
  stringTemplate {functionIdentifierAtom}({anyAtom})
  catchAllAtomDelimiter , 
decrementParser abstractTerminalParser
 cue decrement
 description Decrement
 atoms cueAtom numberIdentifierAtom
 compiler
  stringTemplate {numberIdentifierAtom}--
 popularity .1
dumpIdentifierParser abstractTerminalParser
 cue dumpIdentifier
 description Dump variable(s) to console
 catchAllAtomType identifierAtom
 compiler
  stringTemplate console.log({identifierAtom})
  catchAllAtomDelimiter , 
 popularity .5
exportParser abstractTerminalParser
 cue export
 description Export This
 atoms cueAtom identifierAtom
 compiler
  stringTemplate module.exports = {identifierAtom}
 popularity .1
incrementParser abstractTerminalParser
 cue increment
 description Increment
 popularity .3
 atoms cueAtom numberIdentifierAtom
 compiler
  stringTemplate {numberIdentifierAtom}++
printNumberParser abstractTerminalParser
 cue printNumber
 catchAllAtomType numberIdentifierAtom
 compiler
  stringTemplate console.log({numberIdentifierAtom})
printStringParser abstractTerminalParser
 cue printString
 // todo Allow printing of multiline strings
 catchAllAtomType stringAtomsAtom
 compiler
  stringTemplate console.log("{stringAtoms}")
requireParser abstractTerminalParser
 cue require
 description Require Something
 atoms cueAtom identifierAtom filepathAtom
 compiler
  stringTemplate const {identifierAtom} = require("{filepathAtom}")
 popularity .1
returnParser abstractTerminalParser
 cue return
 atoms cueAtom anyAtom
 compiler
  stringTemplate return {anyAtom}
 popularity .1
hashbangParser
 cue #!
 description Standard bash hashbang line.
 catchAllAtomType hashBangAtom
 compiler
  stringTemplate // #! {hashBangAtom}
 atoms hashBangKeywordAtom
errorParser
 baseParser errorParser
 compiler
  stringTemplate // error