// Atom Parsers
anyAtom
columnNameEnumAtom
columnNameAtom
errorAtom
 paint invalid
intAtom
 paint constant.numeric
onoffAtom
 enum on off
atomAtom
topLevelPropertyAtom
 paint constant.language
opSymbolAtom
 paint keyword.operator.arithmetic

// Line Parsers
jibberishParser
 root
 description A useless Language built for testing Particles code.
 javascript
  execute() {
   return 42
  }
 compilesTo txt
 catchAllParser errorParser
 inScope abstractTopLevelParser textParser abstractBaseClassParser
abstractBaseClassParser
extendsAbstractParser
 atoms topLevelPropertyAtom intAtom
 extends abstractBaseClassParser
 crux extendsAbstract
abstractTopLevelParser
 atoms topLevelPropertyAtom
abstractColorPropertiesParser
 atoms topLevelPropertyAtom intAtom
 extends abstractTopLevelParser
hueParser
 extends abstractColorPropertiesParser
 crux hue
saturationParser
 extends abstractColorPropertiesParser
 crux saturation
constrastParser
 extends abstractColorPropertiesParser
 crux constrast
abstractHtmlParser
 inScope contentParser
 extends abstractTopLevelParser
h1Parser
 crux html.h1
 extends abstractHtmlParser
addParser
 extends abstractTopLevelParser
 crux add
plusParser
 crux +
 extends addParser
 example Adding two numbers:
  + 1 2
 catchAllAtomType intAtom
 atoms opSymbolAtom
blockParser
 inScope abstractTopLevelParser scoreBlockParser
 extends abstractTopLevelParser
 crux block
scoreBlockParser
 description Test that inscope extends and does not overwrite.
 extends blockParser
 inScope scoresParser
 crux scoreBlock
toParser
 atoms topLevelPropertyAtom atomAtom
 compiler
  stringTemplate to {atom}
  closeSubparticles end
 extends blockParser
 crux to
fooParser
 extends abstractTopLevelParser
 crux foo
xColumnNameParser
 description The name of the column to use for the x axis
 atoms topLevelPropertyAtom columnNameEnumAtom
 tags doNotSynthesize
 javascript
  getRunTimeEnumOptions(atom) {
   return atom.atomTypeId === "columnNameEnumAtom" ? ["gender", "height", "weight"] : undefined
  }
 extends abstractTopLevelParser
 crux xColumnName
lightbulbStateParser
 atoms topLevelPropertyAtom onoffAtom
 extends abstractTopLevelParser
 crux lightbulbState
nestedParser
 extends abstractTopLevelParser
 crux nested
particleWithConstsParser
 string greeting hello world
 string singleAtom hello
 string thisHasQuotes "'`
 string longText
  hello
  world
 int score1 28
 int anArray 2 3 4
 float score2 3.01
 boolean win true
 extends abstractTopLevelParser
 crux particleWithConsts
particleExpandsConstsParser
 string greeting hola
 extends particleWithConstsParser
 crux particleExpandsConsts
someCodeParser
 catchAllParser lineOfCodeParser
 extends abstractTopLevelParser
 crux someCode
typeParser
 atoms topLevelPropertyAtom atomAtom
 single
 extends abstractTopLevelParser
 crux type
contentParser
 baseParser blobParser
 crux content
errorParser
 catchAllAtomType errorAtom
 baseParser errorParser
 atoms errorAtom
lineOfCodeParser
 catchAllAtomType atomAtom
textParser
 baseParser blobParser
 crux text
scoresParser
 catchAllAtomType intAtom
 atoms topLevelPropertyAtom
 crux scores