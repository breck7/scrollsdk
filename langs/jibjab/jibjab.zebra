// Cell Parsers
anyCell
columnNameEnumCell
columnNameCell
errorCell
 highlightScope invalid
intCell
 highlightScope constant.numeric
onoffCell
 enum on off
wordCell
topLevelPropertyCell
 highlightScope constant.language
opSymbolCell
 highlightScope keyword.operator.arithmetic
commentCell
 highlightScope comment

// Line Parsers
jibberishParser
 root
 description A useless Tree Language built for testing Tree Notation code.
 javascript
  execute() {
   return 42
  }
 compilesTo txt
 catchAllParser errorParser
 inScope abstractTopLevelParser textParser abstractBaseClassParser
jibjabParser
 root
 description Adds a comment node to Jibberish
 extends jibberishParser
abstractBaseClassParser
extendsAbstractParser
 cells topLevelPropertyCell intCell
 extends abstractBaseClassParser
 crux extendsAbstract
abstractTopLevelParser
 cells topLevelPropertyCell
abstractColorPropertiesParser
 cells topLevelPropertyCell intCell
 extends abstractTopLevelParser
hueParser
 extends abstractColorPropertiesParser
 crux hue
saturationParser
 extends abstractColorPropertiesParser
 crux saturation
constrastParser
 extends abstractColorPropertiesParser
 crux constrast
abstractHtmlParser
 inScope contentParser
 extends abstractTopLevelParser
h1Parser
 crux html.h1
 extends abstractHtmlParser
addParser
 extends abstractTopLevelParser
 crux add
plusParser
 crux +
 extends addParser
 example Adding two numbers:
  + 1 2
 catchAllCellType intCell
 cells opSymbolCell
blockParser
 inScope abstractTopLevelParser scoreBlockParser
 extends abstractTopLevelParser
 crux block
scoreBlockParser
 description Test that inscope extends and does not overwrite.
 extends blockParser
 inScope scoresParser
 crux scoreBlock
toParser
 cells topLevelPropertyCell wordCell
 compiler
  stringTemplate to {word}
  closeChildren end
 extends blockParser
 crux to
fooParser
 extends abstractTopLevelParser
 crux foo
xColumnNameParser
 description The name of the column to use for the x axis
 cells topLevelPropertyCell columnNameEnumCell
 tags doNotSynthesize
 javascript
  getRunTimeEnumOptions(cell) {
   return cell.cellTypeId === "columnNameEnumCell" ? ["gender", "height", "weight"] : undefined
  }
 extends abstractTopLevelParser
 crux xColumnName
lightbulbStateParser
 cells topLevelPropertyCell onoffCell
 extends abstractTopLevelParser
 crux lightbulbState
nestedParser
 extends abstractTopLevelParser
 crux nested
nodeWithConstsParser
 string greeting hello world
 string singleCell hello
 string thisHasQuotes "'`
 string longText
  hello
  world
 int score1 28
 int anArray 2 3 4
 float score2 3.01
 boolean win true
 extends abstractTopLevelParser
 crux nodeWithConsts
nodeExpandsConstsParser
 string greeting hola
 extends nodeWithConstsParser
 crux nodeExpandsConsts
someCodeParser
 catchAllParser lineOfCodeParser
 extends abstractTopLevelParser
 crux someCode
typeParser
 cells topLevelPropertyCell wordCell
 single
 extends abstractTopLevelParser
 crux type
commentParser
 extends abstractTopLevelParser
 catchAllCellType commentCell
 catchAllParser commentParser
 crux comment
contentParser
 baseParser blobParser
 crux content
errorParser
 catchAllCellType errorCell
 baseParser errorParser
 cells errorCell
lineOfCodeParser
 catchAllCellType wordCell
textParser
 baseParser blobParser
 crux text
scoresParser
 catchAllCellType intCell
 cells topLevelPropertyCell
 crux scores