// Cell Parsers
floatCell
commentCell
 highlightScope comment
keywordCell
hashBangKeywordCell
 extends keywordCell
 highlightScope comment
commentKeywordCell
 extends keywordCell
 highlightScope comment
 enum comment
errorCell
 highlightScope invalid
numberCell
 highlightScope constant.numeric
 extends floatCell
numbersCell
 extends numberCell
operatorCell
 highlightScope keyword.operator.arithmetic

// Line Parsers
numbersParser
 root
 description A useless Tree Language for testing Tree Notation features.
 inScope abstractArithmeticReducerParser commentParser hashBangParser
 catchAllParser errorParser
 javascript
  execute() {
   return this.map(child => child.execute())
  }
abstractArithmeticReducerParser
 description First reduces any child lists to one number and then reduces its own lists to one number using provided operator.
 javascript
  execute() {
   return this.numbersCell.slice(1).reduce((curr, tot) => eval(`${curr}${this.operator}${tot}`), this.numbersCell[0])
  }
 inScope abstractArithmeticReducerParser commentParser
 cells operatorCell
 catchAllCellType numbersCell
modParser
 crux %
 extends abstractArithmeticReducerParser
 string operator %
timesParser
 crux *
 extends abstractArithmeticReducerParser
 string operator *
addParser
 crux +
 extends abstractArithmeticReducerParser
 string operator +
substractParser
 crux -
 extends abstractArithmeticReducerParser
 string operator -
divideParser
 crux /
 extends abstractArithmeticReducerParser
 string operator /
commentParser
 description This is a line comment.
 catchAllCellType commentCell
 catchAllParser commentContentParser
 cells commentKeywordCell
commentContentParser
 catchAllCellType commentCell
 catchAllParser commentContentParser
hashBangParser
 crux #!
 cells hashBangKeywordCell
 catchAllCellType commentCell
errorParser
 catchAllCellType errorCell
 baseParser errorParser
 cells errorCell